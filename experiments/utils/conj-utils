;; get-variables function is a function that we use to extract variables from a given patterns (expressions with variables)
;; get-variables helps as an interface for variable-extractor , which is the main function that extracts the variables
;; example:- (get-variables (this is the pattern i am talking about its name is $name and author is $authorName))
;; the above function will call (variable-extractor this (is the pattern i am talking about its name is $name and author is $authorName) ())
;; and finally this will return ($name $authorName) 

( = (variable-extractor $head () $answer)  
    (if (== (get-metatype $head) Variable)
        ( union-atom $answer ($head))
        (if (== (get-metatype $head) Expression)
            (variable-extractor (car-atom $head) (cdr-atom $head) $answer )    
            $answer
        ) 
    )
)
( = (variable-extractor $head $tail $answer) 
    (
        if (== (get-metatype $head) Variable)
            (variable-extractor (car-atom $tail) (cdr-atom $tail) ( union-atom $answer ($head) ) )
            (
                if (== (get-metatype $head) Expression)
                    ( 
                        let* 
                            ( 
                                ;;;;; get all variables from this expression as well as post expression variables
                                ($var1 (variable-extractor (car-atom $head) (cdr-atom $head) () ) )
                                ($var2 (variable-extractor (car-atom $tail) (cdr-atom $tail) () ) )
                                )

                            (   ;;;; merge all variables u have got so far
                                let 
                                    $first_unity (union-atom $answer $var1)
                                    (union-atom $first_unity $var2)
                            )
                            
                        
                    )
                    ( variable-extractor (car-atom $tail) (cdr-atom $tail) $answer )          
            )
    )
)

( = (get-variables $var) (variable-extractor (car-atom $var) (cdr-atom $var) () ) )
;test !( get-variables (Inheritance $new ( $x $y ($again ( $3rd_expression ) ) ) (((text )))$other $r hi there $hi))



;;;;;; depth generator ;;;;;; 
;; input: 3
;; output: (S (S (S ())))
  
( = (generateDepth $size) 
    (
        if (== $size 0)
            ()
            (S (generateDepth (- $size 1)))
    )
) 

;;;;; checks membership of something in a given set of things
;; input: 2-params
  ;; 1st:- $name
  ;; 2nd:- ($var1 $name $var2 $authorName)
;; output: True ;since $name is found in 2nd Parameter

( = (does-exist $unknown $storage)
    (
        if (== $storage ())
            False
            (
                let*
                    (
                        ($top (car-atom $storage))
                        ($leftExpression (cdr-atom $storage))
                    )
                    
                    (
                        if (== $top $unknown)
                            True
                            (does-exist $unknown $leftExpression)
                    )
            )
    )
) 

;;;;;; checker checks whether the combo given has at-least one element from set of variables of pattern 1, return (empty) if not
;; input: 3-params
  ;; 1st:- ($name $var1)
  ;; 2nd:- ($name $var1)
  ;; 3rd:- ($var1 $var2 $authorName)
;; output: 1st parameter ;since $var1 is found in 3rd Parameter
;; we have superposed over the list of combinations so for invalid combos (empty) will close the branch which is made by superpose

( = (checker $the_real_combo $combo $var1)
    (
        if (== $combo ())
            (empty)
            (
                let*
                    (
                        ($headOfCombo (car-atom $combo))
                        ($tailOfCombo (cdr-atom $combo))
                    )
                    (
                        if (does-exist $headOfCombo $var1)  
                            $the_real_combo
                            (checker $the_real_combo $tailOfCombo $var1 )
                    )
            )
         
    )
)  

;;;;; interface with the checker function 
;; input: 2-params
  ;; 1st:- ($name $var1)
  ;; 2nd:- ($var1 $var2 $authorName)
;; output: ($name $var1) ;by calling checker function above

( = (purifier $current $var1) (checker $current $current $var1) ) 


;;;; combination maker;;;;;
;; input: 6-params
  ;; 1st:- ()
  ;; 2nd:- (($a 1 pat1) ($1 1 pat2) ($2 2 pat2) ($3 3 pat2)) ;; ($a 1 pat1) means $a whose index is 1 is from pattern1 ....
  ;; 3rd:- (S (S ()))
  ;; 4th:- 0 ;pointer 1, this shows the recent value ,from patter1 , that we use for this particular combination
  ;; 5th:- 0 ;pointer 2, this shows the recent value ,from patter2 , that we use for this particular combination
  ;; 6th:- 1 ; this shows how much i have in specific combination, till now.
;; in the process any combinations with un-ordered or repeated variables will be pre killed since we got (empty) for such cases
;; output: (($2 $a $3) ($2 $3 $a) ($1 $a $3) ($1 $a $2) ($1 $2 $a) ($1 $3 $a) ($a $1 $3) ($a $1 $2) ($a $2 $3)) 

( = (combiner $accumulated $vars () $p1 $p2 $comboCount) $accumulated )
;;;;; makes combinations by pre-killing what is going to make repetition or what is un-ordered
( = (combiner $accumulated $vars (S $depth) $pointer1 $pointer2 $comboCount) 
    (
        let*
            (
                (($randomVar $index $patName) (superpose $vars))
                ($shouldBePreKilled  
                    (
                        if (== $patName pat1)
                            ( >= $pointer1 $index)
                            (or (>= $pointer2 $index) (not (== $index $comboCount)) )  
                    ) 
                )
                ($joiner (union-atom $accumulated ($randomVar) ) ) 
            )
            (
                if $shouldBePreKilled
                    (empty)
                    (
                        if (== $patName pat1) 
                            (combiner $joiner $vars $depth $index $pointer2 (+ 1 $comboCount))
                            (combiner $joiner $vars $depth $pointer1 $index (+ 1 $comboCount))
                    )
            ) 
    )
)


;;;;; variable formatter ;;;;;;;
;; input: 3-params
  ;; 1st:- () ;; storage for answer
  ;; 2nd:- ($a $b) ;; vars to be formatted
  ;; 3rd:- pat1 ;; where we get this vars from
;; output: (($a 1 pat1) ($b 2 pat2)) 

( = (formatter $accumulated $vars $index $patName)
    (
        if (== $vars ())
            $accumulated
            (
                let*
                    (
                        ($top (((car-atom $vars) $index $patName )))
                        ($tail (cdr-atom $vars))
                    )
                    (formatter (union-atom $accumulated $top) $tail (+ $index 1) $patName) 
            )
    )
)

;;;;; all-variable-combination --the main function ;;;;;;;;;;
;; input: 2-params
  ;; 1st:- ($a $b)
  ;; 2nd:- ($1 $2) 
;; the output will follow the following rules
  ;; atleast 1 variable from pattern1 must appear in the combination
  ;; the combination sizes should be equal to the size of pattern2's variable size
  ;; the order of variables should be maintained with respect to their pattern Name Type ;; i.e we can't say ($b $a) but we can say ($b $1)
;; output: (($2 $a) ($2 $b) ($1 $a) ($1 $b) ($b $1) ($b $2) ($a $b) ($a $1) ($a $2)) 

( = (all-variable-combination $pattern1 $pattern2)
    (
        let*
            (
                ($varsInPat1 (get-variables $pattern1))
                ($varsInPat2 (get-variables $pattern2)) 

                ($first (formatter () $varsInPat1 1 pat1))
                ($second (formatter () $varsInPat2 1 pat2))
                ($joiner (union-atom $first $second))
                
                ($width (size-atom $varsInPat2)) 
                ($unpurifiedAllCombinations (collapse (combiner () $joiner (generateDepth $width) 0 0 1)) )   
            )
            
            ( let $var1Exist (collapse (purifier (superpose $unpurifiedAllCombinations) $varsInPat1)) $var1Exist )   
    )
)

;test by running this !(all-variable-combination (hi this is $a) (i am $1 years $2 $3 living in city) ) 







;;;;;;;;;;;;;;; words sorting in MeTTa ;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;; create new space and store letters with their index on it ;;;;;
!(bind! &newSpace (new-space))
!(add-reducts &newSpace (
   ('a' 1) ('A' 1) ('b' 2) ('B' 2) ('c' 3) ('C' 3) ('d' 4) ('D' 4) ('e' 5) ('E' 5) 
   ('f' 6) ('F' 6) ('g' 7) ('G' 7) ('h' 8) ('H' 8) ('i' 9) ('I' 9) ('j' 10) ('J' 10) 
   ('k' 11) ('K' 11) ('l' 12) ('L' 12) ('m' 13) ('M' 13) ('n' 14) ('N' 14) 
   ('o' 15) ('O' 15) ('p' 16) ('P' 16) ('q' 17) ('Q' 17) ('r' 18) ('R' 18) 
   ('s' 19) ('S' 19) ('t' 20) ('T' 20) ('u' 21) ('U' 21) ('v' 22) ('V' 22) 
   ('w' 23) ('W' 23) ('x' 24) ('X' 24) ('y' 25) ('Y' 25) ('z' 26) ('Z' 26)
))



;;;;; separate function ;;;;;
;; input: 4-parameter
    ;; 1st-param: ($a $b $c $d)
    ;; 2nd-param: 2  ; mid-number 
    ;; 3rd-param: () ; accumulator
    ;; 4th-param: 0  ; how many elements is there in 3rd param
;; output: ( ($a $b) ($c $d) )
( = (separate $given $mid $acc $curr)
    (
        if (== $curr $mid)
            ($acc $given)
            ( let ($top $tail) (decons-atom $given) ( separate $tail $mid (union-atom $acc ($top)) (+ $curr 1) ) )
    )
) 

;;;; slice to half function
;; interface to separate function accepts list of variables and returns what we got from separate function
;; input: ($a $b $x $y) 
;; output: (separate ($a $b $x $y) 2 () 0)
( = (sliceToHalf $given)
    (
        let*
            (
                ($size (size-atom $given))
                ($mid (+ (/ $size 2) (% $size 2)))
            )
            (separate $given $mid () 0)
    )
)

;;;; who is first function ;;;;;;
;; compares whether 2 words and compare which one comes first in alphabetical order 
;; input: 2 params
    ;; 1st-param: ('z' 'e' 'b' 'r' 'a')
    ;; 2nd-param: ('a' 'p' 'p' 'l' 'e')
;; output: second ;; since apple comes before zebra alphabetically 
( = (whoIsFirst $word1 $word2) ;handle first incase they are equal
    (
        if (or (== $word1 ()) (== $word2 ()))
            (if (== $word1 ()) first second)
            (
                let*
                    (   
                        (($firstLetter1 $restLetter1) (decons-atom $word1))
                        (($firstLetter2 $restLetter2) (decons-atom $word2))
                        ($currentLetterNumber1 (match &newSpace ($firstLetter1 $num1) $num1))
                        ($currentLetterNumber2 (match &newSpace ($firstLetter2 $num2) $num2)) 
                    )
                    (
                        if (< $currentLetterNumber1 $currentLetterNumber2)
                            first
                            (
                                if (> $currentLetterNumber1 $currentLetterNumber2)
                                    second
                                    (whoIsFirst $restLetter1 $restLetter2)
                            )
                    )
            )
        
    )
)
 
;;;; merge function ;;;;
;; get 2 sorted expressions and return 1 sorted expression (by merging the two)
;; input: 3 parameters
    ;; 1st-param: ( ('a' 'b' 'a' 'y') ('r' 'i' 'v' 'e' 'r'))
    ;; 2nd-param: ( ('b' 'r' 'i' 'd' 'g' 'e') ('d' 'a' 'm'))
    ;; 3rd-param: () ;answer storage
;; output: ( ('a' 'b' 'a' 'y') ('b' 'r' 'i' 'd' 'g' 'e') ('d' 'a' 'm') ('r' 'i' 'v' 'e' 'r') )
( = (merge $left $right $store)
    (  
        if (or (== (size-atom $left) 0) (== (size-atom $right) 0))
            (
                if (== (size-atom $left) 0)
                    (union-atom $store $right) 
                    (union-atom $store $left)  
            )
            (let*
                ( 
                    (($leftCurr $leftLeft)  (decons-atom $left))
                    (($rightCurr $rightRight) (decons-atom $right)) 
                    ($sameWord (noreduce-eq $leftCurr $rightCurr))  
                )
                ( 
                    if $sameWord
                        ( let $joint (union-atom ($leftCurr) ($rightCurr)) (merge $leftLeft $rightRight (union-atom $store $joint)) )
                        (
                            if (== (whoIsFirst $leftCurr $rightCurr) first) 
                                (merge $leftLeft $right (union-atom $store ($leftCurr))) 
                                (merge $left $rightRight (union-atom $store ($rightCurr))) 
                        )
                )
            )
    )
)


;;;; merge-sort function ;;;;
;; accepts one expression and sorts it
;; input: ( ('b' 'r' 'i' 'd' 'g' 'e') ('a' 'b' 'a' 'y')  ('r' 'i' 'v' 'e' 'r') ('d' 'a' 'm') )
;; output: ( ('a' 'b' 'a' 'y') ('b' 'r' 'i' 'd' 'g' 'e') ('d' 'a' 'm') ('r' 'i' 'v' 'e' 'r') ) ;; by splitting , sorting and merging recursively
( = (merge_sort $toBeSorted)
    (
        if (> 2 (size-atom $toBeSorted))
            $toBeSorted
            (let*
                (
                    ($sliced (sliceToHalf $toBeSorted))
                    ($leftPart (car-atom $sliced))
                    ($right (cdr-atom $sliced))
                    ($rightPart (car-atom $right))   

                    ; ($sortedLeft (merge_sort $leftPart))
                    ; ($sortedRight (merge_sort $rightPart)) 
                    ;; using superpose will help us to parallarly sort the left and right wings
                    (($sortedLeft $sortedRight) (collapse (superpose ( (merge_sort $leftPart) (merge_sort $rightPart) ))))


                    ($answer (merge $sortedLeft $sortedRight ()))

                )
                $answer 
            )
    )
)
 

;;;; chars To Atom Converter ;;;;
;; input: 2 parameters
    ;; 1st-param: ( ('a' 'b' 'a' 'y') ('b' 'r' 'i' 'd' 'g' 'e') ('d' 'a' 'm') ('r' 'i' 'v' 'e' 'r') )
    ;; 2nd-param: ()
;; output: (abay bridge dam river)
( = (to-Atom $word $finalAnswer) 
    (
        if (== $word ())
            $finalAnswer
            (
                let*
                    ( 
                        (($head $tail) (decons-atom $word))
                        ($parsedHead ((parse (charsToString $head))))
                        ($ansJoin (union-atom $finalAnswer $parsedHead))
                    )
                    (to-Atom $tail $ansJoin)
            )
    )
)

;;;; explode function ;;;;
;; input: abay
;; output: ('a' 'b' 'a 'y)
( = (explode $word) (stringToChars (repr $word) ) )

;;;; sort function ;;;;
;; input: (abay is the longest river)
;; output: (abay is longest river the)
( = (sort $toBeSorted)
    (
        let*
            (
                ($explodedWords (collapse (explode (superpose $toBeSorted))))
                ($sortedWords (merge_sort $explodedWords)) 
            )
            (to-Atom $sortedWords ())
    )
)

;test be running this !(sort (hey i am Yonas a Student))   
